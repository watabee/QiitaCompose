// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    apply from: rootProject.file("dependabot/build.gradle")

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Deps.androidGradlePlugin
        classpath Deps.Kotlin.gradlePlugin
        classpath Deps.Dagger.Hilt.androidGradlePlugin
        classpath Deps.spotlessGradlePlugin
        classpath Deps.secretsGradlePlugin
    }
}

allprojects {
    apply from: rootProject.file("dependabot/build.gradle")

    repositories {
        google()
        mavenCentral()
        jcenter()

        // Used for Accompanist snapshots
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
        }
    }
}

subprojects { subproject ->
    apply plugin: "com.diffplug.spotless"

    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
        }
    }

    afterEvaluate {
        tasks.withType(KotlinCompile).configureEach {
            it.dependsOn spotlessApply
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
