plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id 'kotlin-kapt'
}

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    lintOptions {
        abortOnError false
    }
    namespace 'com.github.watabee.qiitacompose.ui.common'

    testOptions {
        managedDevices {
            devices {
                pixel4api30 (com.android.build.api.dsl.ManagedVirtualDevice) {
                    // Use device profiles you typically see in Android Studio.
                    device = "Pixel 4"
                    // Use only API levels 27 and higher.
                    apiLevel = 30
                    // To include Google services, use "google".
                    systemImageSource = "aosp"
                }
            }
            groups {
                ci {
                    targetDevices.add(devices.pixel4api30)
                }
            }
        }
    }
}

dependencies {
    api projects.baseAndroid
    implementation projects.data

    implementation libs.androidx.appcompat
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.core.ktx

    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.compose.constraintlayout
    implementation libs.androidx.navigation.compose

    api libs.androidx.lifecycle.runtime.ktx

    api libs.material

    api libs.androidx.hilt.navigation.compose

    implementation libs.timber

    coreLibraryDesugaring libs.desugarJdkLibs

    testImplementation libs.junit
    testImplementation libs.truth
    testImplementation libs.androidx.test.junit.ktx

    debugImplementation libs.androidx.compose.ui.test.manifest
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    androidTestImplementation libs.truth
}
